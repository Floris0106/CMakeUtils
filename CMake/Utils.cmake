function(start_timer)
    string(TIMESTAMP START_TIME "%s%f")
    set(START_TIME ${START_TIME} PARENT_SCOPE)
endfunction()

function(stop_timer ELAPSED)
    string(TIMESTAMP END_TIME "%s%f")
    math(EXPR DIFFERENCE "(${END_TIME} - ${START_TIME} + 50000) / 100000")
    math(EXPR SECONDS "${DIFFERENCE} / 10")
    math(EXPR FRACTION "${DIFFERENCE} % 10")
    set(${ELAPSED} "${SECONDS}.${FRACTION}s" PARENT_SCOPE)
endfunction()

function(get_target_sources TARGET SOURCES)
    get_target_property(TARGET_SOURCES ${TARGET} SOURCES)
    set(FILES)
    foreach (SOURCE ${TARGET_SOURCES})
        get_source_file_property(SOURCE_LANGUAGE ${SOURCE} LANGUAGE TARGET_DIRECTORY ${TARGET})
        get_source_file_property(SOURCE_LOCATION ${SOURCE} LOCATION TARGET_DIRECTORY ${TARGET})
        if (${SOURCE_LANGUAGE} MATCHES "C(XX)?")
            list(APPEND FILES "${SOURCE_LOCATION}")
        endif ()
    endforeach ()
    set(${SOURCES} ${FILES} PARENT_SCOPE)
endfunction()